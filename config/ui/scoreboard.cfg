///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////
newui "scoreboard" [
    if $mainmenu [hideui "scoreboard"]
    if (= $getmode 0) [hideui "scoreboard"] //no scoreboard in edit
    uiallowinput 0
    refreshscoreboard
    uicolor (? $sbtransparent $c_menu_a $c_menu) 0 0 [
        uivlist 0 [
            UI_sbheader
            uifill 0 $pad_thin
            UI_playertableteam
            UI_spectatorlist
        ]
    ]
] [if $mainmenu [hideui "scoreboard"]]

///////////////////////////////////////////////////////////////////////////////

UI_sbheader = [
    uifill 0 $pad_thin
    uifill $UI_sbwteam  $pad_medium [
        uifill (-f $UI_sbwteam (? (= $showip 1) 0.18 0)) 0 [
            uialign -1
            uiclip (-f $UI_sbwteam (? (= $showip 1) 0.18 0)) 0 [
                uialign -1
                // uispace 0.01 0 [
                uispace $pad_thin 0 [
                    uivlist 0 [
                        if $scoreboardservinfo [
                            uitext $scoreboardservinfo $title_height
                        ] [
                            uitext "^f4Imprimis" $title_height
                        ]
                        uihlist $reg_width [
                            uifill
                            uihlist $pad_thin [
                                uitext "^fs[" $sbmidtext
                                uitext (concatword (at ["^f0" "^f0" "^f2" "^f3"] $getmastermode) (getmastermodename $getmastermode)) $sbmidtext
                                uitext "^fS]" $sbmidtext
                            ]
                            UItriangle $color_light_gray $triangle_width $triangle_height 270
                            uitext (getmodeprettyname $getmode) $sbmidtext
                            UItriangle $color_light_gray $triangle_width $triangle_height 270
                            uitext $scoreboardmap $sbmidtext
                            if (MODE_TIMED $getmode) [
                                UItriangle $color_light_gray $triangle_width $triangle_height 270
                                uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) $sbmidtext
                            ]
                        ]
                        uialign* -1
                    ]
                ]
            ]
        ]
        if $scoreboardmultiplayer [
            if $showip [
                uifill 0.18 0 [
                    uialign 1
                    UIbar 0 1; uialign- -1
                    uiclip 0.18 0 [
                        uialign 1
                        uispace 0.01 0 [
                            uivlist 0.004 [
                                uicolortext $connectedip $color_mid_gray $sbmidtext
                                uicolortext $connectedport $color_mid_gray $sbmidtext
                                uialign* 1
                            ]
                        ]
                        uialign- 1
                    ]
                ]
            ]
        ]
    ]
]
//-------------------------------------------------------------------------------------------------

UI_playertableteam = [
    uifill 0 0 [
        uihlist 0 [
            uitable 0 0 [
                uialign 0 -1
                uitableheader [
                    uifill $reg_width
                    if $scoreboardmultiplayer [
                        if $showclientnum [uifill $UI_cw_cn $reg_height [uicolortext "#"     $color_mid_gray $sblittletext ; uialign- -1 1]]
                        if $showping      [uifill $UI_cw_p  $reg_height [uicolortext "P"     $color_time $sblittletext ; uialign- -1 1]]
                        if $showpj        [uifill $UI_cw_pj $reg_height [uicolortext "PJ"    $color_time $sblittletext ; uialign- -1 1]]
                    ]
                    if $showdeaths       [uifill $UI_cw_d  $reg_height [uicolortext "D"     $color_d $sblittletext ; uialign- -1 1]]
                    if $showkills        [uifill $UI_cw_k  $reg_height [uicolortext "K"     $color_k $sblittletext ; uialign- -1 1]]
                    if $showscore        [uifill $UI_cw_s  $reg_height [uicolortext "Score" $color_score $sblittletext ; uialign- -1 1]]
                    uifill (UI_cw_n) $reg_height [uicolortext (getteamscore 1) 0x4060D0 1 ; uialign- 1]
                    uifill $reg_width
                ] [uicolor 0x99202860 0 0 [uiclamp 1 1 1 1]]
                UI_sbtc = 0
                loopscoreboard cn 1 [
                    UI_sbtc = (! $UI_sbtc)
                    uitablerow [
                        uifill $reg_width
                        if $scoreboardmultiplayer [
                            if $showclientnum [uifill $UI_cw_cn $row_height [uicolortext (? (isai $cn) "^f4-" $cn)                  $color_mid_gray $sbmidtext ; uialign- -1]]
                            if $showping      [uifill $UI_cw_p  $row_height [uicolortext (? (isai $cn) "^f4-" (scoreboardping $cn)) $color_time $sbmidtext ; uialign- -1]]
                            if $showpj        [uifill $UI_cw_pj $row_height [uicolortext (? (isai $cn) "^f4-" (scoreboardpj $cn))   $color_time $sbmidtext ; uialign- -1]]
                        ]
                        if $showdeaths       [uifill $UI_cw_d  $row_height [uicolortext (getclientdeaths $cn)                      $color_d $sbmidtext ; uialign- -1]]
                        if $showkills        [uifill $UI_cw_k  $row_height [uicolortext (getclientfrags  $cn)                      $color_k $sbmidtext ; uialign- -1]]
                        if $showscore        [uifill $UI_cw_s  $row_height [uicolortext (getclientflags  $cn)                      $color_score $sbmidtext ; uialign- -1]]
                        uifill (UI_cw_n) $row_height [uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) $sbbigtext ; uialign- 1]
                        uifill $reg_width
                    ] [
                        uicolor (? $UI_sbtc 0x99333b40 0x99262b33) 0 0 [uiclamp 1 1 1 1]
                        pushif highlight (scoreboardhighlight $cn) [uioutline $color_mid_gray (-f (*f $UI_sbwteam 0.5) 0.002) 0.024]
                    ]
                ]
            ]
            uitable 0 0 [
                uialign 0 -1
                uitableheader [
                    uifill $reg_width
                    uifill (UI_cw_n) $reg_height [uicolortext (getteamscore 2) 0xD04040 1 ; uialign- -1]
                    if $showscore        [uifill $UI_cw_s  $reg_height [uicolortext "Score" $color_score $sblittletext ; uialign-  1 1]]
                    if $showkills        [uifill $UI_cw_k  $reg_height [uicolortext "K"     $color_k $sblittletext ; uialign-  1 1]]
                    if $showdeaths       [uifill $UI_cw_d  $reg_height [uicolortext "D"     $color_d $sblittletext ; uialign-  1 1]]
                    if $scoreboardmultiplayer [
                        if $showpj        [uifill $UI_cw_pj $reg_height [uicolortext "PJ"    $color_time $sblittletext ; uialign-  1 1]]
                        if $showping      [uifill $UI_cw_p  $reg_height [uicolortext "P"     $color_time $sblittletext ; uialign-  1 1]]
                        if $showclientnum [uifill $UI_cw_cn $reg_height [uicolortext "#"     $color_mid_gray $sblittletext ; uialign-  1 1]]
                    ]
                    uifill $reg_width
                ] [uicolor 0x99602020 0 0 [uiclamp 1 1 1 1]]
                UI_sbtc = 0
                loopscoreboard cn 2 [
                    UI_sbtc = (! $UI_sbtc)
                    uitablerow [
                        uifill $reg_width
                        uifill (UI_cw_n) $row_height [uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) $sbbigtext ; uialign- -1]
                        if $showscore        [uifill $UI_cw_s  $row_height [uicolortext (getclientflags  $cn)                      $color_score $sbmidtext ; uialign-  1]]
                        if $showkills        [uifill $UI_cw_k  $row_height [uicolortext (getclientfrags  $cn)                      $color_k $sbmidtext ; uialign-  1]]
                        if $showdeaths       [uifill $UI_cw_d  $row_height [uicolortext (getclientdeaths $cn)                      $color_d $sbmidtext ; uialign-  1]]
                        if $scoreboardmultiplayer [
                            if $showpj        [uifill $UI_cw_pj $row_height [uicolortext (? (isai $cn) "^f4-" (scoreboardpj $cn))   $color_time $sbmidtext ; uialign-  1]]
                            if $showping      [uifill $UI_cw_p  $row_height [uicolortext (? (isai $cn) "^f4-" (scoreboardping $cn)) $color_time $sbmidtext ; uialign-  1]]
                            if $showclientnum [uifill $UI_cw_cn $row_height [uicolortext (? (isai $cn) "^f4-" $cn)                  $color_mid_gray $sbmidtext ; uialign-  1]]
                        ]
                        uifill $reg_width
                    ] [
                        uicolor (? $UI_sbtc 0x99403333 0x99332626) 0 0 [uiclamp 1 1 1 1]
                        pushif highlight (scoreboardhighlight $cn) [uioutline $color_mid_gray (-f (*f $UI_sbwteam 0.5) 0.002) 0.024]
                    ]
                ]
            ]
        ]
    ]
]
//-------------------------------------------------------------------------------------------------
UI_spectatorlist = [
    uicolor (? $sbtransparent $c_menu_a $c_menu) 0 0.022 [
        uiclamp 1 1 1 1
        if $showspectators [
            uigrid (? (> $getmode 0) 3 2) 0 0 [
                loopscoreboard cn -1 [
                    uispace 0.01 0.01 [
                        pushif sbhigh (scoreboardhighlight $cn) [
                            uioutline $color_mid_gray ; uiclamp- 1 1 1 1
                        ]
                        uihlist 0.01 [
                            uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) $sbbigtext
                            if $scoreboardmultiplayer [if $showclientnum [uitext $cn $sbbigtext]]
                        ]
                    ]
                ]
            ]
        ]
    ]
]
